apply plugin: 'com.android.application'
apply plugin: "kotlin-android"

android {
    // defaults
    compileSdkVersion Integer.valueOf(PARAM_COMPILE_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS

    defaultConfig {
        minSdkVersion Integer.valueOf(PARAM_MIN_SDK_VERSION)
        targetSdkVersion Integer.valueOf(PARAM_TARGET_SDK_VERSION)

        versionCode 17
        versionName '0.8'
    }

    // exclude from result
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    // compile options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // set lint in project
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    // signing of versions
    signingConfigs {
        debug {
            storeFile file(String.valueOf(System.getenv("ANDROID_SIGN_DEBUG_STORE_FILE")))
            storePassword System.getenv("ANDROID_SIGN_DEBUG_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_SIGN_DEBUG_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_SIGN_DEBUG_KEY_PASSWORD")
        }
        release {
            storeFile file(String.valueOf(System.getenv("ANDROID_SIGN_RELEASE_STORE_FILE")))
            storePassword System.getenv("ANDROID_SIGN_RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_SIGN_RELEASE_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_SIGN_RELEASE_KEY_PASSWORD")
        }
    }

    // building task
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    // product flavors
    flavorDimensions "basic"
    productFlavors {
        addonGraphHopper {
            applicationId "com.asamm.locus.addon.graphhopper"
            dimension "basic"
        }
    }
}

dependencies {
    // Kotlin
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + KOTLIN_VERSION

    // Locus API
    implementation 'com.asamm:locus-api-android:' + LIB_LOCUS_API

    // Android support libraries
    implementation 'com.android.support:appcompat-v7:' + LIB_ANDROID_SUPPORT

    // add GraphHopper dependency
    // https://github.com/graphhopper/graphhopper/tree/master/android
    // https://mvnrepository.com/artifact/com.graphhopper/graphhopper-core
    implementation(group: 'com.graphhopper', name: 'graphhopper-core', version: '1.0-pre20') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'org.openstreetmap.osmosis', module: 'osmosis-osm-binary'
        exclude group: 'org.apache.xmlgraphics', module: 'xmlgraphics-commons'
    }
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.slf4j:slf4j-android:1.7.25'
}
